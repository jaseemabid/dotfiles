# https://just.systems
# Dependencies: op (1Password CLI), openssl, jq, python3, gzip

# Show available commands
@_default:
    just -u --list

# Decode a rayconfig file to JSON file
decode file:
    #!/usr/bin/env bash
    set -euo pipefail
    if [[ ! -f "{{ file }}" ]]; then
        echo "Error: File not found: {{ file }}" >&2
        exit 1
    fi
    just _decode-json "{{ file }}" > "$(basename "{{ file }}" .rayconfig).json"

# Extract keybindings with decoded keycodes from a rayconfig file
keycodes file:
    @just _decode-json "{{ file }}" | ./keycodes.py

# Get the decryption password
@_password:
    op read "op://Private/Raycast Settings Export/password"

# Decrypt and decompress rayconfig to JSON
_decode-json file:
    #!/usr/bin/env bash
    set -euo pipefail
    PASSWORD=$(just _password)
    openssl enc -d -aes-256-cbc -nosalt -in "{{ file }}" -k "$PASSWORD" 2>/dev/null \
        | tail -c +17 \
        | gunzip \
        | jq .
